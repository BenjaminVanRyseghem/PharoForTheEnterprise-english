\ifx\wholebook\relax\else

	% Lulu:
	\documentclass[a4paper,10pt,twoside]{book}
	\usepackage[
		papersize={6.13in,9.21in},
		hmargin={.75in,.75in},
		vmargin={.75in,1in},
		ignoreheadfoot
	]{geometry}
	\input{../common.tex}
	\setboolean{lulu}{true}
% --------------------------------------------
% A4:
%	\documentclass[a4paper,11pt,twoside]{book}
%	\input{../common.tex}
%	\usepackage{a4wide}
% --------------------------------------------
    \graphicspath{{figures/} {../figures/}}
	\begin{document}
\fi
\sloppy
\chapter{Fuel}

Fuel is an open-source general-purpose object serialization framework.

\section{General Information}

\subsection{Goals}

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ Concrete }
\item  We don't aspire to have a dialect-interchange format. This enables us to serialize special objects like contexts, block closures, exceptions, compiled methods and classes. Although there are ports to other dialects, Fuel development is Pharo-centric.
\end{list}

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ Flexible}
\item  Depending on the context, there could be multiple ways of serializing the same object. For example, a class can be considered either a global or a regular object. In the former case, it will be encoded just its name; in the latter case, the class will be encoded in detail, with its method dictionary, etc.
\end{list}

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ Fast }
\item  We worry about to have the best performance. We developed a complete benchmark suite to help analyse the performance with diverse sample sets, as well as compare against other serializers. Our pickling algorithm allows outstanding materialization performance, as well as very good serialization performance too.
\end{list}

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ Object-Oriented Design}
\item  From the beginning it was a constraint to have a good object-oriented design and to do not need any special support from the VM. In addition, Fuel has a complete test suite, with a high coverage. We also worry about writing comments on classes and methods. 
\end{list}

\subsection{Features}

\begin{itemize}
\item  Is a fast, concrete, general-purpose and flexible binary serializer.
\item  Object-Oriented design.
\item  No special VM-support needed.
\item  Modular (clear division in packages).
\item  Can serialize$/$materialize not only plain objects but also classes, traits, methods, closures, contexts, packages, etc.
\item  Support for global references.
\item  Very customizable: ignore certain instance variables, substitute objects by others, pre and post serialization and materialization actions, etc.
\item  Supports class rename and class reshape.
\item  Good test coverage (almost 600 unit tests).
\item  Large suite of benchmarks.
\end{itemize}

\subsection{Development}

\subsection{Some links}

\begin{itemize}
\item  \href{http://code.google.com/p/fuel/issues/list}{Issue tracker}
\item  \href{http://ss3.gemstone.com/ss/Fuel.html}{Source repository}
\item  \href{http://ci.inria.fr/pharo-contribution/job/Fuel/}{Continuous integration}
\end{itemize}

\subsection{Collaborators}
\begin{itemize}
\item  Martin Dias - tinchodias (at) gmail (dot) com (Developer)
\item  Mariano Martinez Peck - marianopeck (at) gmail (dot) com (Developer)
\item  Max Leske (Developer)
\item  Pavel Krivanek (Developer)
\item  Tristan Bourgois (Past Developer)
\item  Stephane Ducasse (Promotor and financer)
\end{itemize}



%=========================================================================% 
\section{Installation}

\label{Installation}

Fuel 1.9 is already loaded in Pharo 2.0. The \textbf{default packages} work out of the box in Pharo 1.1.1, 1.1.2, 1.2, 1.3, 1.4, 2.0 and Squeak 4.1, 4.2, 4.3, 4.4.

\subsection{Default}

For most users should be enough to install just the default packages:

\Needspace{3\baselineskip}
\begin{lstlisting}
Gofer it
    squeaksource: 'MetacelloRepository';
    package: 'ConfigurationOfFuel';
    load.
((Smalltalk at: #ConfigurationOfFuel) project version: '1.9') 
    load.
\end{lstlisting}


\subsection{Basic demo}

Open the \ct{Transcript} and evaluate code below in a \ct{Workspace}.

\Needspace{3\baselineskip}
\begin{lstlisting}
| serializedArray materializedArray |
serializedArray := 
    Array 
        with: (Set with: 42)
        with: Transcript
        with: [:aString | Transcript show: aString; cr ].

"Store (serialize)"
FLSerializer serialize: serializedArray toFileNamed: 'demo.fl'. 

"Load (materialize)"
materializedArray := FLMaterializer materializeFromFileNamed: 'demo.fl'. 

Transcript
	show: 'The sets are equal: ';
	show: serializedArray first = materializedArray first;
	cr;
	show: 'But not the same: ';
	show: serializedArray first ~~ materializedArray first;
	cr;
	show: 'The global value Transcript is the same: ';
	show: serializedArray second == materializedArray second;
	cr.
materializedArray third 
	value: 'The materialized block closure can be properly evaluated.'.
\end{lstlisting}



%=========================================================================% 
\section{Getting Started}

\label{GettingStarted}

\subsection{Basic examples}

We give some class-side messages to facilitate the more common uses of serialization and materialization. The next example writes and reads from file:

\Needspace{2\baselineskip}
\begin{lstlisting}
FLSerializer serialize: 'stringToSerialize' toFileNamed: 'demo.fuel'. 
materializedString := FLMaterializer materializeFromFileNamed: 'demo.fuel'.
\end{lstlisting}

We also provide messages for storing into a \ct{ByteArray}. This can be fine for users of a NoSQL database:

\Needspace{2\baselineskip}
\begin{lstlisting}
anArray := FLSerializer serializeToByteArray: 'stringToSerialize'. 
materializedString := FLMaterializer materializeFromByteArray: anArray.
\end{lstlisting}


\subsection{FileStream}

In this example we work with files. Note that we set the file in binary mode:

\Needspace{3\baselineskip}
\begin{lstlisting}
FileStream forceNewFileNamed: 'demo.fuel' do: [:aStream |
	FLSerializer newDefault 
		serialize: 'stringToSerialize' 
 		on: aStream binary].

FileStream oldFileNamed: 'demo.fuel' do: [:aStream |
	materializedString := (FLMaterializer newDefault 
		materializeFrom: aStream binary) root].
\end{lstlisting}

Note also that we are no longer using the class-side messages of previous examples. Now, for both \ct{FLSerializer} and \ct{FLMaterializer}, we first create instances with \ct{\#newDefault} to then perform the desired operations. As we will see in next example, creating the instances brings more possibilities.

\subsection{Compressing}

Of course, you could use stream compressors provided by the system. However, we have detected some errors serializing \ct{WideString}s. An example of use:

\Needspace{3\baselineskip}
\begin{lstlisting}
FileStream forceNewFileNamed: 'number.fuel.zip' do: [:aFileStream | 
	|gzip|
	aFileStream binary.
	gzip := GZipWriteStream on: aFileStream.
	FLSerializer newDefault serialize: 123 on: gzip.
	gzip close. ].
\end{lstlisting}
	
\Needspace{3\baselineskip}
\begin{lstlisting}
FileStream oldFileNamed: 'number.fuel.zip' do: [:aFileStream | 
	|gzip|
	aFileStream binary.
	gzip := GZipReadStream on: aFileStream.		
	materialization := FLMaterializer newDefault 
		materializeFrom: gzip.
	gzip close. ].
\end{lstlisting}

\subsection{Showing a progress bar}

Sometimes it is nice to see progress updates on screen. Use \ct{\#showProgress} in this cases.

\Needspace{3\baselineskip}
\begin{lstlisting}
FileStream forceNewFileNamed: 'numbers.fuel' do: [:aStream |
	FLSerializer newDefault 
		showProgress;
		serialize: (1 to: 200000) asArray 
		on: aStream binary ].
	
FileStream oldFileNamed: 'numbers.fuel' do: [:aStream | 
	FLMaterializer newDefault 
		showProgress;
		materializeFrom: aStream binary ].
\end{lstlisting}

Package \ct{FuelProgressUpdate} must be installed. You can use:

\Needspace{2\baselineskip}
\begin{lstlisting}
(ConfigurationOfFuel project version: '1.9') 
	load: 'FuelProgressUpdate'.
\end{lstlisting}


%=========================================================================% 
\section{Managing Globals}
\label{ManagingGlobals}

Let us assume a \ct{CompiledMethod} is referenced from the graph to serialize. Sometimes we may be interested in storing just the selector and name of the class, because we know it will be present when materializing the graph. However, sometimes we want to really store the method with full detail.

This means that given an object graph, there is not an unique way of serializing it. Fuel offers dynamic and static mechanisms to customize this.

\subsection{Default globals}

By default, Fuel considers following objects as globals, i.e. will store just its name:

\begin{itemize}
\item  \ct{nil}, \ct{true}, \ct{false}, and \ct{Smalltalk globals}.
\item  Any \ct{Class}, \ct{Trait}, \ct{Metaclass} or \ct{ClassTrait}.
\item  Any \ct{CompiledMethod} (except when either it \ct{\#isInstalled} not or \ct{\#isDoIt}, for example, the code is evaluated from Workspace).
\item  Some well-known global variables: \ct{Smalltalk} \ct{SourceFiles} \ct{Transcript} \ct{Undeclared} \ct{Display} \ct{TextConstants} \ct{ActiveWorld} \ct{ActiveHand} \ct{ActiveEvent} \ct{Sensor} \ct{Processor} \ct{ImageImports} \ct{SystemOrganization} \ct{World}.
\end{itemize}

\subsection{Custom globals are duplicated}

In this following code snippet we show that by default the global value is not serialized as a global, and so it is duplicated on materialization.

\Needspace{3\baselineskip}
\begin{lstlisting}
 "Define a global variable named #SomeGlobal."
 SomeGlobal := Set new.
 
 "Serialize and materialize the value of #SomeGlobal."
 FLSerializer 
 	serialize: SomeGlobal 
 	toFileNamed: 'g.fuel'.
 
 "The materialized object *is not* the same as the global instance."
 [ (FLMaterializer materializeFromFileNamed: 'g.fuel') ~~ SomeGlobal ] assert.
\end{lstlisting}

But...

\subsection{How to avoid duplication}

Instead, in the code below \ct{\#considerGlobal:} is used to specify that it should be stored as global.

\Needspace{3\baselineskip}
\begin{lstlisting}
 | aSerializer |
 
 "Define a global variable named #SomeGlobal."
 SomeGlobal := Set new.
 
 aSerializer := FLSerializer newDefault.
 
 "Tell the serializer to consider #SomeGlobal as global."
 aSerializer analyzer considerGlobal: #SomeGlobal.
 
 aSerializer 
 	serialize: SomeGlobal 
 	toFileNamed: 'g.fuel'.
 	
 "In this case, the materialized object *is* the same as the global instance."
 [ (FLMaterializer materializeFromFileNamed: 'g.fuel') == SomeGlobal ] assert.
\end{lstlisting}

This feature is tested in \ct{tests-globals} protocol of \ct{FLBasicSerializationTest} as well in \ct{FLGlobalEnvironmentTest}.

\subsection{Changing the environment}

It is possible to specify where the global will be looked-up during materialization. The method \ct{\#globalEnvironment:} exists for that purpose, as the following example shows. 

\Needspace{3\baselineskip}
\begin{lstlisting}
 | aSerializer aMaterializer anEnvironment |
 
 "Define a global variable named #SomeGlobal."
 SomeGlobal := Set new.
 
 "Tell the serializer to consider #SomeGlobal as global."
 aSerializer := FLSerializer newDefault.
 aSerializer analyzer considerGlobal: #SomeGlobal.
 aSerializer 
 	serialize: SomeGlobal 
 	toFileNamed: 'g.fuel'.
 
 "Override value for #SomeGlobal."
 anEnvironment := Dictionary newFrom: Smalltalk globals.
 anEnvironment at: #SomeGlobal put: {42}.
 
 "In this case, the materialized object *is the same* as the global instance."
 FileStream oldFileNamed: 'g.fuel' do: [ :aStream |
 	aStream binary.
 	aMaterializer := FLMaterializer newDefault.
 	
 	"Set the environment"
 	aMaterializer globalEnvironment: anEnvironment.
 
 	[ (aMaterializer materializeFrom: aStream) root = {42} ] assert ]
\end{lstlisting}

This feature is tested in the class \ct{FLGlobalEnvironmentTest}. The global environment can be setted also for serialization (not only materialization), but we don't include an example for that case.


%=========================================================================% 
\section{Customizing the Graph}

\subsection{Ignoring Instance Variables}

It can happen that instance variables should never be serialized. A practical way to do this is overriding the hook method \ct{\#fuelIgnoredInstanceVariableNames}. 

Let's say we have the class \ct{User} and we do not want to serialize the instance variables \ct{'acumulatedLogins'} and \ct{'applications'}. So we implement:

\Needspace{2\baselineskip}
\begin{lstlisting}
User class >> fuelIgnoredInstanceVariableNames
    ^#('acumulatedLogins' 'applications')
\end{lstlisting}

When materialized, such instance variables will be nil. If you want to re-initialize and set values to those instance variables, you can use \ct{\#fuelAfterMaterialization} for that.

Be aware that in case of renaming those instance variables, you should rename that method as well. Notice also that the method \ct{\#fuelIgnoredInstanceVariableNames} is implemented at class side. This means that \textbf{all} instances of such class will ignore the defined instances variables. 

We test this feature in \ct{FLIgnoredVariablesTest}.

In \ct{StOMP} serializer this same hook is called \ct{\#stompTransientInstVarNames} and in \ct{SIXX} it is \ct{\#sixxIgnorableInstVarNames}. 

\subsection{Post-Materialization Action}

The method \ct{\#fuelAfterMaterialization} let us execute something once an object has been materialized. For example, let's say we would like to set back the instance variable \ct{'acumulatedLogins'} during materialization. Hence, we can implement:

\Needspace{2\baselineskip}
\begin{lstlisting}
User >> fuelAfterMaterialization
 acumulatedLogins := 0. 
\end{lstlisting}

\subsection{Substitution on Serialization}

Sometimes you may want to serialize something different than the original object, without altering them.

\subsubsection{Dynamic way}

You can establish a pluggable substitution to a particular serialization. 

Let's illustrate with an example, where your graph includes a \ct{Stream} and you want to serialize \ct{nil} instead. 

\Needspace{3\baselineskip}
\begin{lstlisting}
objectToSerialize := Array with: 'hello' with: '' writeStream.

FileStream forceNewFileNamed: 'demo.fuel' do: [ :aStream |
    aSerializer := FLSerializer newDefault.
    aSerializer analyzer 
        when: [ :o | o isStream ] 
        substituteBy: [ :o | nil ].
    aSerializer         
        serialize: objectToSerialize
        on: aStream binary ].
\end{lstlisting}

So, when loading you will get \ct{\#('hello' nil)}, without any instance of a stream.

You can find this code in \ct{FLUserGuidesTest$>$$>$testPluggableSubstitution}.

\subsubsection{Static way}

You have to override \ct{\#fuelAccept:} in the class of the object to be substituted. Fuel visits each object in the graph by sending this message, to determine how to trace and serialize it. Note that this will affect every serialization, in contrast with the 'dynamic way' we explained above; but it could be much faster.

As an example, imagine we want to replace an object directly with nil. In other words, we want to make a whole object transient, say \ct{CachedResult}. For that, we should implement:

\Needspace{2\baselineskip}
\begin{lstlisting}
CachedResult >> fuelAccept: aGeneralMapper
    ^ aGeneralMapper visitSubstitution: self by: nil
\end{lstlisting}

As another example, we have a \ct{Proxy} class and when serializing we want to serialize its \ct{target} instead of the proxy. So we implement:

\Needspace{2\baselineskip}
\begin{lstlisting}
Proxy >> fuelAccept: aGeneralMapper
    ^ aGeneralMapper visitSubstitution: self by: target
\end{lstlisting}

Notice that \ct{\#fuelAccept:} is the same as the previous example. The last example is when an object needs to change the value of its instance variables. Say we have again the class \ct{User} and we want to \ct{nil} the instance variable \ct{'history'} when its size is greater than 100. 

\Needspace{3\baselineskip}
\begin{lstlisting}
User >> fuelAccept: aGeneralMapper
    ^self history size > 100 
        ifTrue: [ 
            aGeneralMapper 
                visitSubstitution: self 
                by: (self copy history: Array new) ].
        ifFalse: [ super fuelAccept: aGeneralMapper ]
\end{lstlisting}

\textbf{Note} we are substituting the original user by another instance of \ct{User}, which Fuel will visit with the same \ct{\#fuelAccept:} method. We could easily fall in an infinite sequence of substitutions if we don't take care. To avoid this problem, it is useful \ct{\#visitSubstitution:by:onRecursionDo:}, where you define an alternative mapping for the case of mapping an object which is already a substitute of another one:

\Needspace{3\baselineskip}
\begin{lstlisting}
User >> fuelAccept: aGeneralMapper
    aGeneralMapper 
        visitSubstitution: self 
        by: (self copy history: #())
        onRecursionDo: [ super fuelAccept: aGeneralMapper ]
\end{lstlisting}

In the case, the substitute user (i.e. the one with the empty history) is will be visited via its super implementation.

You can see tests for this functionality at \ct{FLHookedSubstitutionTest}.

\subsection{Substitution on Materialization}

\subsubsection{Global Sends}

Suppose we have a special instance of \ct{User} that represents the admin user, and it is an unique instance in the image. In case the admin user is referenced in our graph, we want to treat that object as a global. We can do that in this way:

\Needspace{3\baselineskip}
\begin{lstlisting}
User >> fuelAccept: aGeneralMapper
    ^self == User admin
        ifTrue: [ 
            aGeneralMapper 
                visitGlobalSend: self 
                name: #User 
                selector: #admin ]
        ifFalse: [ super fuelAccept: aGeneralMapper ]
\end{lstlisting}

So what will happen is that during serialization, the admin user won't be completly serialized (with all its intance variables) but instead its global name and selector are stored. Then, at materialization time, Fuel will send \ct{\#admin} to the class \ct{User}, and use what that answers as the admin user of the materialized graph. 

We test this feature in \ct{FLGlobalSendSerializationTest}.

\subsubsection{Hooking instance creation}

Fuel provides two hook methods to customise how instances are created: \ct{\#fuelNew} and \ct{\#fuelNew:}.

For (regular) fixed objects, the method \ct{\#fuelNew} is defined in \ct{Behavior} as:

\Needspace{2\baselineskip}
\begin{lstlisting}
fuelNew
	^ self basicNew
\end{lstlisting}

But we can override it to our needs, for example:

\Needspace{2\baselineskip}
\begin{lstlisting}
fuelNew
	^ self uniqueInstance
\end{lstlisting}

This similarly applies to variable objects through the method \ct{\#fuelNew:}, which by default answers \ct{\#basicNew:}.

We test this feature in \ct{FLSingletonTest}.

\subsection{Not Serializable Objects}

You may want to be sure that some objects are not serialized. For this case we provide \ct{\#visitNotSerializable:}, which in next example forbids serialization of any instance of \ct{MyNotSerializableObject}.

\Needspace{2\baselineskip}
\begin{lstlisting}
MyNotSerializableObject >> fuelAccept: aGeneralMapper
    aGeneralMapper visitNotSerializable: self
\end{lstlisting}

We test this feature in \ct{FLBasicSerializationTest$>$$>$testNotSerializableObject}.


%=========================================================================% 
\section{Errors}

We provide a hierarchy of errors which allow to clearly identify the problem if something went wrong:

\begin{itemize}
\item  \ct{FLError}\begin{itemize}
\item  \ct{FLSerializationError}\begin{itemize}
\item  \ct{FLNotSerializable}
\item  \ct{FLObjectNotFound}
\item  \ct{FLObsolete}
\end{itemize}

\item  \ct{FLMaterializationError}\begin{itemize}
\item  \ct{FLBadSignature}
\item  \ct{FLBadVersion}
\item  \ct{FLClassNotFound}
\item  \ct{FLGlobalNotFound}
\item  \ct{FLMethodChanged}
\item  \ct{FLMethodNotFound}
\end{itemize}

\end{itemize}

\end{itemize}

As most classes of Fuel, they have class comments that give an idea their meanings:

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ FLError}
\item  I represent an error produced during Fuel operation.
\end{list}

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ FLSerializationError}
\item  I represent an error happened during serialization.
\end{list}

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ FLNotSerializable}
\item  I represent an error which may happen while tracing in the graph an object that is forbidden of being serialized.
\end{list}

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ FLObjectNotFound}
\item  I represent an error which may happen during serialization, when trying to encode on the stream a reference to an object that should be encoded before, but it is not. This usually happens when the graph changes during serialization. Another possible cause is a bug in the analysis step of serialization.
\end{list}

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ FLObsolete}
\item  I am an error produced during serialization, signaled when trying to serialize an obsolete class as global. It is a prevention, because such class is likely to be absent during materialization.
\end{list}

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ FLMaterializationError}
\item  I represent an error happened during materialization.
\end{list}

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ FLBadSignature}
\item  I represent an error produced during materialization when the serialized signature doesn't match the materializer's signature (accessible via \ct{FLMaterializer$>$$>$signature}). A signature is a byte prefix that should prefix a well-serialized stream.
\end{list}

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ FLBadVersion}
\item  I represent an error produced during materialization when the serialized version doesn't match the materializer's version (accessible via \ct{FLMaterializer$>$$>$version}). A version is encoded in 16 bits and is encoded heading the serialized stream, after the signature.
\end{list}

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ FLClassNotFound}
\item  I represent an error produced during materialization when a serialized class or trait name doesn't exist.
\end{list}

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ FLGlobalNotFound}
\item  I represent an error produced during materialization when a serialized global name doesn't exist (at \ct{Smalltalk globals}).
\end{list}

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ FLMethodChanged}
\item  I represent an error produced during materialization when is detected a change in the bytecodes of a method serialized as global. This error was born when testing the materialization of a \ct{BlockClosure} defined in a method that changed. The test produced a VM crash.
\end{list}

\begin{list}{}{}
\item\hspace{-\labelwidth}\textbf{ FLMethodNotFound}
\item  I represent an error produced during materialization when a serialized method in a class or trait name doesn't exist (at \ct{Smalltalk globals}).
\end{list}


%=========================================================================% 
\section{Object Migration}

Often, we need to load objects whose class has changed since it was saved. In this document how to load them in the different cases. The figure below is useful to explain some of them. Imagine we serialized an instance of \ct{Point} and we need to materialize it when \ct{Point} class has changed.

\begin{todo}
Insert here: figures$/$ClassChanges.png
\end{todo}
%+figures/ClassChanges.png+
Let's start with the easier cases. If a variable was \textbf{inserted}, its value will be \ct{nil}. If \textbf{removed}, it is also obvious: the serialized value will be ignored. In the case the variables are the same the the \textbf{order changed}, Fuel also tolerates it automatically.

A more interesting case is when a variable was \textbf{renamed}, where the user can map old names to new ones. In our example:

\Needspace{3\baselineskip}
\begin{lstlisting}
FLMaterializer newDefault
	migrateClassNamed: #Point
	variables: {'x' -> 'posX'. 'y' -> 'posY'}.
\end{lstlisting}

Not surprisingly, if nothing specified the change will be understood by Fuel as two independent operations, an insertion and a removal.

The last change in the figure is a \textbf{class rename}. This should be specified this way:

\Needspace{3\baselineskip}
\begin{lstlisting}
FLMaterializer newDefault
	migrateClassNamed: #Point
	toClass: Coordinate.
\end{lstlisting}

It is also available \ct{\#migrateClassNamed:toClass:variables:} to combine both \textbf{class and variable rename}.

Although not illustrated in the figure, a class could also change its \textbf{layout}. For example, Point could change from being \textbf{fixed} to \textbf{variable}. This should be also automatically tolerated by Fuel. Unfortunately, the inverse (variable to fixed) is not supported so far.

You can find tests related to this guide in \ct{FLMigrationTest}.

Additionally, the method \ct{globalEnvironment:}, showed in \ref{ManagingGlobals}, might be useful for migrations: you can prepare an ad-hoc environment dictionary with the same keys that were used during serialization, but with the new classes as values.


%=========================================================================% 
\section{Fuel Format Migration}

Until now, each Fuel version has its own stream format. Furthermore, each version is \textbf{not} compatible with the others. This means that when upgrading Fuel version, we will need to convert our serialized streams. 

We include below an example of migration. Let's say we have some files serialized with Fuel 1.7 in a Pharo 1.4 image and we want to migrate them to Fuel 1.9.

\Needspace{3\baselineskip}
\begin{lstlisting}
 | oldVersion newVersion fileNames objectsByFileName 
   materializerClass serializerClass |
 oldVersion := '1.7'.
 newVersion := '1.9'.
 fileNames := #('a.fuel' 'b.fuel' 'c.fuel' 'd.fuel' 'e.fuel').
 objectsByFileName := Dictionary new.
 

 (ConfigurationOfFuel project version: oldVersion) load.
 materializerClass := Smalltalk at: #FLMaterializer.
 
 fileNames do: [ :fileName | 
 	objectsByFileName 
 		at: fileName 
 		put: (materializerClass materializeFromFileNamed: fileName) ].
 
 
 (ConfigurationOfFuel project version: newVersion) load.
 serializerClass := Smalltalk at: #FLSerializer.
 
 objectsByFileName keysAndValuesDo: [ :fileName :objects |
 	serializerClass 
 		serialize: objects  
 		toFileNamed: 'migrated-', fileName
 	 ].
\end{lstlisting}

\begin{note}
Note 1: We assume in this example that the number of objects to migrate can be materialized all together at the same time. This can be false. In such case, we could fix the script to split the list of files and do it in parts.
\end{note}

\begin{note}
Note 2: It is necessary to fetch the classes in the System Dictionary after the desired Fuel version has been loaded.
\end{note}

\begin{note}
Note 3: This script should be evaluated in the original image. For example, we don't guarantee that Fuel 1.7 loads in Pharo 2.0, but we know that Fuel 1.9 loads in Pharo 1.4.
\end{note}


%=========================================================================% 
\section{Debugging}

There are a couple of packages that help us debugging Fuel. To understand the output of the tools in this guide, you should know some basics of how Fuel internally works. 

\subsection{Serialization}
The most important thing to remark is that serialization is split in two main steps: analysis and encoding.

\subsubsection{Analysis}
It consists in a graph iteration, mapping each traversed object to its correspondent grouping, called \textbf{cluster}. 

\subsubsection{Encoding}
After analysis, we linearly write on the stream, in these steps:

\begin{enumerate}
\item header
\item for each cluster, instances part
\item for each cluster, references part
\item trailer
\end{enumerate}

\subsection{Materialization}
It consists on progressively recreating the graph.

\subsubsection{Decoding}
This is done by linearly reading from the stream. So, steps are obviously analogous to the ones above:

\begin{enumerate}
\item header
\item for each cluster, instances part
\item for each cluster, references part
\item trailer
\end{enumerate}

\subsection{Debug Tools}
Ensure you have them with:

\Needspace{2\baselineskip}
\begin{lstlisting}
(ConfigurationOfFuel project version: '1.8.1') 
    load: #(FuelDebug FuelPreview).
\end{lstlisting}

Next, a transcript of some useful class comments. 

\subsubsection{FLGraphViewBuilder}
I add draw capabilities to analysis in \ct{FuelDebug} package.

Right-click a node for inspect it. Some examples:

\Needspace{3\baselineskip}
\begin{lstlisting}
(FLAnalyzer newDefault
    setDebug;
    analysisFor: #((1) (2) (3) (4)))
    open.
\end{lstlisting}

\Needspace{3\baselineskip}
\begin{lstlisting}
(FLAnalyzer newDefault
    setDebug;
    analysisFor: #((1) (2) (3) (4)))
    openPathsTo: 3.
\end{lstlisting}
    
\Needspace{3\baselineskip}
\begin{lstlisting}
(FLAnalyzer newDefault
    setDebug;
    analysisFor: #((1) (2) (3) (4)))
    openPathsToEvery: [:o | o isNumber and: [o > 2] ].
\end{lstlisting}

They look like:

\begin{todo}
Insert here: figures$/$FuelPreview
\end{todo}
%+figures/FuelPreview+
\subsubsection{FLDebugSerialization}
I am a serialization which facilitates debugging, by logging the stream position before and after main steps of \ct{FLSerialization}, including cluster information. Obviously, you should be familiar with such class and the algorithm to understand the output log.

To use, send \ct{\#setDebug} to your serializer and run as usually. For example:

\Needspace{3\baselineskip}
\begin{lstlisting}
FileStream forceNewFileNamed: 'debug.fuel' do: [:aFile |
    FLSerializer newDefault
        setDebug;
        serialize: ''hello'' on: aFile binary ]
\end{lstlisting}

Then, inspect the output log:

\Needspace{1\baselineskip}
\begin{lstlisting}
FLDebugSerialization last log
\end{lstlisting}

\subsubsection{FLDebugMaterialization}
I am a materialization which facilitates debugging, by logging the stream position before and after main steps of \ct{FLMaterialization}, including cluster information. Obviously, you should be familiar with such class and the algorithm to understand the output log.

To use, send \ct{\#setDebug} to your serializer and run as usually. For example:

\Needspace{3\baselineskip}
\begin{lstlisting}
FileStream oldFileNamed: 'debug.fuel' do: [:aFile |
    FLMaterializer newDefault
        setDebug;
        materializeFrom: aFile binary ]
\end{lstlisting}

Then, inspect the output log:

\Needspace{1\baselineskip}
\begin{lstlisting}
FLDebugMaterialization last log
\end{lstlisting}


%=========================================================================% 
\section{Built-in Header Support}

Since the graph of objects serialized in a file can be large, and it can be useful to query some small extra info, Fuel supports the possibility to easily add such information in a header. The following examples show this set of features:
 
\Needspace{3\baselineskip}
\begin{lstlisting}
| serializer |
serializer := FLSerializer newDefault.

serializer header 
	at: #timestamp
	putAdditionalObject: TimeStamp now.

serializer header
	addPreMaterializationAction: [ 
		Transcript show: 'Before serializing'; cr ].

serializer header
	addPostMaterializationAction: [ :materialization | 
		Transcript 
			show: 'Serialized at ';
			show: (materialization additionalObjectAt: #timestamp); 
			cr;
			show: 'Materialized at ';
			show: TimeStamp now; 
			cr ].
	
serializer 
	serialize: 'a big amount of data' 
	toFileNamed: 'demo.fl'
\end{lstlisting}

Then, you can just materialize the header info:

\Needspace{3\baselineskip}
\begin{lstlisting}
| aHeader |
aHeader := FLMaterializer materializeHeaderFromFileNamed: 'demo.fl'.
aHeader additionalObjectAt: #timestamp.
\end{lstlisting}

Printing it, the result is:

\Needspace{1\baselineskip}
\begin{lstlisting}
'28 March 2013 12:44:54 pm'
\end{lstlisting}

If we normally materialize the whole file with:

\Needspace{1\baselineskip}
\begin{lstlisting}
FLMaterializer materializeFromFileNamed: 'demo.fl' 
\end{lstlisting}

Then, the print of the results is:

\Needspace{1\baselineskip}
\begin{lstlisting}
'a big amount of data'
\end{lstlisting}

And this is shown in \ct{Transcript}:

\Needspace{3\baselineskip}
\begin{lstlisting}
Before serializing
Serialized at 28 March 2013 12:50:50 pm
Materialized at 28 March 2013 1:01:21 pm
\end{lstlisting}
 
For additional examples, you can see tests in \ct{FLHeaderSerializationTest}.
\ifx\wholebook\relax\else
   \bibliographystyle{jurabib}
   \nobibliography{scg}
\end{document}
\fi
