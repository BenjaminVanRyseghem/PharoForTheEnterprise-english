!Crafting a little Embedded DSL


In this chapter we will develop a simple domain specific language (DSL) for rolling dice. Players of games such as Dungeon and Dragons are familiar with the DSL we will implement. An example of such DSL is ==2 D20 + 1 D6== which means that we should roll two times a 20-faces dice and one time a 6 faces dice. This chapter will show how we can (1) simply reuse traditional operator such as ==+==, (2) develop an embedded DSL and (3) show a nice usage of class extensions.

!! Getting started

Using the code browser, define a package named ==Dice== or any name your like.

!!! Defining the class Dice


[[[
Object subclass: #Dice
	instanceVariableNames: 'faces'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Dice'
]]]

In the ==initialize== category, define the method ==initialize== as follows. It simply set the default number of faces to 6. 

[[[
Dice>>initialize 
	super initialize.
	faces := 6
]]]


!! Creating a test 

It is always empowering to verify that the code we write is always working as we defining it. For 
this purpose we will create a unit test. Remember unit testing was promoted by K. Beck first in Smalltalk. Nowadays this is a common practice but this is always useful to remember our roots!

So we define the class ==DiceTest== as a subclass of ==TestCase==.

[[[
TestCase subclass: #DiceTest
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Dice'
]]]


[[[
DiceTest>>testInitializeIsOk

		self shouldnt: [ Dice new ] raise: Error
]]]

!! Rolling a dice
To roll a dice we will use the method from Number ==atRandom== which draws randomly a 
number between one and the receiver. For example ==10 at random== draws number between 1 to 10. 
Therefore we define the method ==roll== as follows.

[[[
Dice>>roll

	^ faces atRandom
]]]

Now we can create an instance ==Dice new== and send it the message  ==roll== and get a result.
Do ==Dice new inspect== and then type in the bottom pane ==self roll==.



!! Creating another test
We will define a test that verifies that rolling a new created dice with a default 6 faces only returns 
value comprised between 1 and 6. This is what the following test method is actually specifying.
 
[[[
DiceTest>>testRolling
	
	| d |
	d := Dice new. 
	10 timesRepeat: [ self assert: (d roll between: 1 and: 6) ]
]]]

Note that often it is better to define the test even before the code it tests. Why? Because you can think about the API of your objects and a scenario that illustrate their correct behavior. It helps you to program your solution.

!! Instance creation interface.
We would like to get a simpler way to create Dice. For example we want to create a 20-faces dice as follows: ==Dice faces: 20==. Let us define a test for it.

[[[
DiceTest>>testCreationIsOk
	
		self shouldnt: [ Dice faces: 20 ] raise: Error
]]]


We define the class method ==faces:== as follows. It creates an instance then send the message ==faces:== to it and returns the instance.

[[[
Dice>>faces: aNumber

	| instance |
	instance := self new.
	instance faces: aNumber.
	^ instance
]]]

This method is strictly equivalent to the one below. The trick is that ==yourself== is a simple method defined on ==Object== class. ==yourself== returns the receiver of a message and the use of ==;== sends the message to the receiver of the previous message (here ==faces:==), therefore ==yourself== is sent to the object resulting from the execution of the expression ==self new== (which returns a new instance of the class ==Dice==). 

[[[
Dice>>faces: aNumber
	
	^ self new faces: aNumber; yourself
]]]

If you execute it will not work since we did not create yet the method ==faces:== this is now the time to define it.
[[[
Dice>>faces: aNumber

	faces := aNumber
]]]

Now your tests should run. 

So even if the class ==Dice== could implement more behavior, we are ready to implement a dice handle. 

!! Handle of Dice

Let us define a new class ==DiceHandle== that represents a dice handle. 
This class defines one instance variables to hold dices it contains. 

[[[
Object subclass: #DiceHandle
	instanceVariableNames: 'dice'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Dice'
]]]

[[[
DiceHandle>>initialize
	super initialize.
	dice := OrderedCollection new.
]]]

[[[
DiceHandle>>addDice: aDice 
	dice add: aDice
]]]

!!! Creating a Handle

[[[
DiceHandle new 
	addDice: (Dice faces: 6);
	addDice: (Dice faces: 10);
	yourself
]]]

Now when you open an inspector you cannot see well the dice. so we will enhance the ==printOn:== method.


[[[
Dice>>printOn: aStream

	super printOn: aStream.
	aStream nextPutAll: ' (', faces printString, ')'
]]]


!!! Testing Handle Dice
[[[
TestCase subclass: #DiceHandleTest
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'RolePlaying'
]]]

[[[
DiceHandleTest>>testCreationIsOk

	| handle |
	handle := DiceHandle new 
			addDice: (Dice faces: 6);
			addDice: (Dice faces: 10);
			yourself.
	self assert: handle diceNumber = 2.
]]]

Now we should add the method diceNumber to the ==DiceHandle==
[[[
DiceHandle>>diceNumber  

	^ dice size
]]]

Now we can define the rolling of a handle of dice by simply summing the dice roll.
[[[
DiceHandle>>roll
	
	| res |
	res := 0.
	dice do: [ :each | res := res + each roll ].
	^ res
]]]


Now we can send the message ==roll== to a dice handle.
[[[
handle := DiceHandle new 
		addDice: (Dice faces: 6);
		addDice: (Dice faces: 10);
		yourself.
handle roll
]]]

!! Role playing syntax

Now we are ready to offer a syntax following practice of role playing game, i.e., using ==2 D20== to create a handle of two 20 faces dice.  For this purpose we will define class extensions: we will define methods in the class ==Integer== but these methods will be only available when the package Dice will be loaded. 

But first let us specify  what we would like to obtain by writing a new  test in the class ==DiceHandleTest==. Remember
to always take  any opportunity to write tests.  When we execute ==2 D20== we  should get a new handle  composed of two
dice and can verify that. This is what the method ==testSimpleHandle== is doing.

[[[
DiceHandleTest>>testSimpleHandle

	| handle |
	handle := 2 D20 
	self assert: handle diceNumber = 5.
]]]

Verify that the test is not working! It is much more satisfactory to get a test running when it was not working before. Now define the method \ct{D20} with a category name that is ==*Dice== (if you named your package Dice). This method simply creates a new dice handle, add the correct number of dice to this handle and return it.

[[[
Integer>>D20
	
	| handle |
	handle := DiceHandle new.
	self timesRepeat: [ handle addDice:  (Dice faces: 20)].
	^ handle
]]]

Now you test should pass and this is probably a good moment to save your work either by publishing your package to SmalltalkHub and to save your image. 

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]

[[[
 
]]]



% Local Variables:
% eval: (flyspell-mode -1)
% End:
